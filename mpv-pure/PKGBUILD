# Maintainer: Vasiliy Stelmachenok <ventureo@yandex.ru>
## Arch Linux maintainers:
# Maintainer: Christian Hesse <mail@eworm.de>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Eivind Uggedal <eivind@uggedal.com>

pkgname=mpv-pure
pkgver=0.40.0
pkgrel=4
pkgdesc='a free, open source, and cross-platform media player'
arch=('x86_64')
license=('GPL-2.0-or-later AND LGPL-2.1-or-later')
url='https://mpv.io/'
depends=('desktop-file-utils' 'ffmpeg' 'glibc' 'hicolor-icon-theme'
  'lcms2' 'libarchive' 'libass' 'libdisplay-info' 'libdrm' 'libegl' 'libgl' 'libglvnd'
  'libjpeg-turbo' 'libpipewire' 'libplacebo' 'libva' 'libxkbcommon'
  'luajit' 'mesa' 'uchardet' 'vulkan-icd-loader' 'wayland' 'zimg' 'zlib')
makedepends=('ffnvcodec-headers' 'git' 'ladspa' 'meson'
  'python-docutils' 'vulkan-headers' 'wayland-protocols')
optdepends=('yt-dlp: for video-sharing websites playback')
conflicts=('libmpv.so' 'mpv')
provides=('libmpv.so' 'mpv')
options=('!emptydirs')
source=("$pkgname-$pkgver::git+https://github.com/mpv-player/mpv.git#tag=v${pkgver}")
sha256sums=('754d045dba7143b51dd6eb2d32ab3a9a483e6a337e685b581a876bc2e20eddf1')

prepare() {
  cd "$pkgname-$pkgver"

  # Fix build with ffmpeg 8
  git cherry-pick -n 26b29fba02a2782f68e2906f837d21201fc6f1b9
}

build() {
  local _meson_options=(
    --buildtype=release
    --auto-features=disabled
    -Dstrip=true
    -Db_lto=true
    -Db_lto_mode=default

    -Dbuild-date=false

    -Dcplugins=enabled
    -Diconv=enabled
    -Djpeg=enabled
    -Dlcms2=enabled
    -Dlibarchive=enabled
    -Dlibavdevice=enabled
    -Dlua=luajit
    -Duchardet=enabled
    -Dvector=enabled
    -Dzimg=enabled
    -Dzlib=enabled

    -Dpipewire=enabled

    -Ddmabuf-wayland=enabled
    -Ddrm=enabled
    -Degl=enabled
    -Degl-drm=enabled
    -Degl-wayland=enabled
    -Dgbm=enabled
    -Dgl=enabled
    -Dvaapi=enabled
    -Dvaapi-drm=enabled
    -Dvaapi-wayland=enabled
    -Dvulkan=enabled
    -Dwayland=enabled

    -Dmanpage-build=enabled
  )

  arch-meson "${pkgname}-${pkgver}" build "${_meson_options[@]}"

  meson compile -C build
}

check() {
  meson test -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"

  # delete private entries only required for static linking
  sed -i -e '/Requires.private/d' -e '/Libs.private/d' "${pkgdir}"/usr/lib/pkgconfig/mpv.pc

  install -m0644 "${pkgname}-${pkgver}"/DOCS/{encoding.rst,tech-overview.txt} \
    "${pkgdir}"/usr/share/doc/mpv

  install -m0644 "${pkgname}-${pkgver}"/TOOLS/{umpv,mpv_identify.sh,stats-conv.py,idet.sh,lua/*} \
    -D -t "${pkgdir}"/usr/share/mpv/scripts
}
