# Maintainer: Vasiliy Stelmachenok <ventureo@yandex.ru>
## Maintainers of original Arch Linux package:
# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

pkgname=mesa-pure
pkgver=24.3.3
pkgrel=1
epoch=1
pkgdesc="A purely personal Mesa build"
url="https://www.mesa3d.org/"
arch=(x86_64)
license=("MIT AND BSD-3-Clause AND SGI-B-2.0")
makedepends=(
    'xorgproto'
    'libxml2'
    'libvdpau'
    'libva'
    'elfutils'
    'libxrandr'
    'meson'
    'ninja'
    'glslang'
    'python-mako'
    'python-ply'
    'cbindgen'
    'wayland-protocols'
    'cmake'
    'valgrind'
    'libpng'
    'python-packaging'
    'python-pyaml'
)
depends=(
    'libdrm'
    'libxxf86vm'
    'libxdamage'
    'libxshmfence'
    'libelf'
    'libunwind'
    'libglvnd'
    'wayland'
    'lm_sensors'
    'vulkan-icd-loader'
    'zstd'
    'expat'
    'gcc-libs'
    'libxfixes'
    'libx11'
    'systemd-libs'
    'libxext'
    'libxcb'
    'glibc'
    'zlib'
    'python'
    'xcb-util-keysyms'
)
optdepends=('opengl-man-pages: for the OpenGL API man pages')
provides=(
    'vulkan-mesa-layers'
    'opencl-driver'
    'opengl-driver'
    'vulkan-driver'
    'vulkan-radeon'
    'libva-mesa-driver'
    'mesa-libgl'
    'mesa'
)
conflicts=(
    'vulkan-mesa-layers'
    'opencl-clover-mesa'
    'vulkan-radeon'
    'libva-mesa-driver'
    'mesa-libgl'
    'mesa'
)
source=(
  "https://mesa.freedesktop.org/archive/mesa-$pkgver.tar.xz"
  d58064d68c7b436ed9c62b020a91ffe646ee60d1.patch
  3df25e76a5e83a42b4db9527ac6ca3cc14c47ae6.patch
  06ea60e4d820ecc058d565d25ff55c66fcc37d65.patch
)
sha256sums=(
  'ad9f5f3a6d2169e4786254ee6eb5062f746d11b826739291205d360f1f3ff716'
  'd1725b68d23abe8e7f75c35a9d494c91c9becb24b37888227fad02e53ec878e9'
  '99247002327d3471cd2d758c0e81483af773f368645a16b0952356243727c28f'
  '30d87cd454f50b32d689fa44339c81deea3f10441c8909bf71b2ce37c2206e09'
)

prepare() {
  cd mesa-$pkgver
  
  # Fix Wine WoW64 performance in GL
  patch -Np1 -i ../d58064d68c7b436ed9c62b020a91ffe646ee60d1.patch
  patch -Np1 -i ../3df25e76a5e83a42b4db9527ac6ca3cc14c47ae6.patch
  patch -Np1 -i ../06ea60e4d820ecc058d565d25ff55c66fcc37d65.patch

  # Include package release in version string so Chromium invalidates
  # its GPU cache; otherwise it can cause pages to render incorrectly.
  # https://bugs.launchpad.net/ubuntu/+source/chromium-browser/+bug/2020604
  echo "$pkgver-arch$epoch.$pkgrel" >VERSION
}

build() {
  local meson_options=(
    -D b_lto=true
    -D c_args="-O3 -march=native"
    -D c_link_args="-O1"
    -D cpp_args="-O3 -march=native"
    -D cpp_link_args="-O1"
    -D llvm=disabled
    -D amd-use-llvm=false
    -D android-libbacktrace=disabled
    -D b_ndebug=true
    -D gallium-drivers=radeonsi,zink
    -D gallium-extra-hud=false
    -D gallium-nine=false
    -D gallium-opencl=disabled
    -D gallium-rusticl=false
    -D gallium-xa=disabled
    -D gallium-vdpau=disabled
    -D gles1=disabled
    -D glx=dri
    -D html-docs=disabled
    -D intel-rt=disabled
    -D libunwind=disabled
    -D microsoft-clc=disabled
    -D osmesa=false
    -D platforms=x11,wayland
    -D valgrind=enabled
    -D video-codecs=all
    -D vulkan-drivers=amd
    -D vulkan-layers=device-select,overlay,screenshot
  )

  # Build only minimal debug info to reduce size
  CFLAGS+=" -g1"
  CXXFLAGS+=" -g1"

  # Inject subproject packages
  export MESON_PACKAGE_CACHE_DIR="$srcdir"

  arch-meson mesa-$pkgver build "${meson_options[@]}"
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"

  # indirect rendering
  ln -s /usr/lib/libGLX_mesa.so.0 "${pkgdir}/usr/lib/libGLX_indirect.so.0"

  install -Dm644 mesa-$pkgver/docs/license.rst -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set sw=2 sts=-1 et:
